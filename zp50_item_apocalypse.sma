/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <amxmisc>
#include <engine>
#include <hamsandwich>
#include <zp50_items>
#include <zp50_ammopacks>
#include <zp50_gamemodes>
#include <screenfade_util>
#include <fun>
#include <zp50_colorchat>
#include <zp50_class_nemesis>
#include <zp50_grenade_frost>
#include <zp50_grenade_fire>
#include <zmvip>

#define PLUGIN "[ZP] Item : Zombie Apocalypse"
#define VERSION "1.0"
#define AUTHOR "zXCaptainXz"

#define TASK_NIGHTVISION 7532415398702
#define TASK_REPLAY 100

native zp_is_blind(id)
native zp_is_nvg_active(id);
native zp_apocalypse_icon_set();
//native zp_nade_bought();
// native zv_get_bought(id);
native zp_items_bought_get(id);

new NvgCounter;
new Item;
new bool:Used;
new bool:Active;
new Normal, Multi;
new HudSync;
new cvar_infection_health;

public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR)	
	RegisterHam(Ham_TakeDamage, "info_target", "fw_TakeDamage");	
	RegisterHam(Ham_TakeDamage, "func_wall", "fw_TakeDamage");
	RegisterHam(Ham_TakeDamage, "func_breakable", "fw_TakeDamage");	
	RegisterHam(Ham_TakeDamage, "player", "fw_TakeDamage");
	Item = zp_items_register("Zombie Apocalypse","Buff All Zombies", 90, 0, 0, 1)
	HudSync = CreateHudSyncObj()
}

public plugin_precache()
{
	precache_sound("ambience/zapmachine_alien.wav")
	precache_sound("zombie_plague/nemesis2.wav")
	precache_sound("ambience/gc_apocalypse.mp3")
} 

public plugin_natives()
{
	register_native("zp_is_apocalypse","native_is_apocalypse",1)
}

public native_is_apocalypse()
{
	return Active;
}

public plugin_cfg()
{
	Normal = zp_gamemodes_get_id("Infection Mode")
	Multi =  zp_gamemodes_get_id("Multiple Infection Mode")
	cvar_infection_health = get_cvar_pointer("zp_infection_health_bonus")
}

public TaskAllow()
{	
	zp_colored_print(0, "^x04Zombie Apocalypse^x03 is available to buy!");
}
public zp_fw_items_select_pre(id, itemid)
{
	if(itemid!=Item)
		return ZP_ITEM_AVAILABLE
	
	if(!zp_core_is_zombie(id))
		return ZP_ITEM_DONT_SHOW
	
	static currentMode;
	currentMode = zp_gamemodes_get_current()
	
	if(currentMode!=Normal&&currentMode!=Multi)
		return ZP_ITEM_DONT_SHOW
	
	if(!(get_user_flags(id)&ADMIN_KICK)&&!(zv_get_user_flags(id)&ZV_MAIN))
	{
		zp_items_menu_text_add("\r[ADMIN/VIP]")
		return ZP_ITEM_NOT_AVAILABLE;
	}

	if(Used)
	{		
		return ZP_ITEM_NOT_AVAILABLE
	}

	// if(zp_nade_bought())
	// {
	// 	zp_items_menu_text_add("(Bought Infection Bomb)")
	// 	return ZP_ITEM_NOT_AVAILABLE
	// }
	
	new Text[32];
	format(Text, charsmax(Text), "[0/1]");
	zp_items_menu_text_add(Text);
	
	return ZP_ITEM_AVAILABLE	
}

public zp_fw_items_select_post(id, itemid)
{	
	if(itemid!=Item)
	{		
		return;
	}
		
	set_hudmessage(255,0,0, -1.0, 0.2, 1)
	new name[32]
	get_user_name(id, name, charsmax(name));
	ShowSyncHudMsg(0, HudSync, "A Zombie Apocalypse led by %s is inbound!^n^nHumans better be ready...",name)
	zp_colored_print(0, "A^x03 Zombie Apocalypse^x01 led by^x03 %s^x01 is inbound! Humans better be ready...",name)
	new health = get_user_health(id)
	zp_class_nemesis_set(id);
	set_user_health(id, 10000+health)
	client_cmd(0, "spk ^"ambience/zapmachine_alien.wav^"");		
	Used=true;	
	Active=true;
	set_task(6.5, "BeginApocalypse")	
}

public BeginApocalypse()
{
	client_cmd(0, "stopsound");	
	static currentMode;
	currentMode = zp_gamemodes_get_current()
	
	if(currentMode!=Normal&&currentMode!=Multi)
		return ;	
	
	zp_apocalypse_icon_set();
	set_hudmessage(255,0,0, -1.0, 0.2, 1,6.0,10.0)
	ShowSyncHudMsg(0, HudSync, "A Zombie Apocalypse has begun! Zombies are now stronger than ever!")
	zp_colored_print(0, "A^x03 Zombie Apocalypse^x01 has begun! Zombies are now stronger than ever!")
	client_cmd(0, "spk ^"zombie_plague/nemesis2.wav^"");
	client_cmd(0,"mp3 play ^"/sound/ambience/gc_apocalypse.mp3^"")
	set_task(210.0,"replay",TASK_REPLAY)
	server_cmd("zp_lighting d")
		
	set_hudmessage(255,0,0, -1.0, 0.2, 1)
	ShowSyncHudMsg(0, HudSync, "The Apocalypse is here!")
	NvgCounter=0;
	set_task(0.1, "custom_nightvision_task", TASK_NIGHTVISION, _, _, "b")
	
	new bought;
	for(new i = 1; i < 33; i++)
	{		
		if(!is_user_connected(i))
		continue;				
		if(!is_user_alive(i))
		continue;
		if(!zp_core_is_zombie(i))
		{
			bought=zp_items_bought_get(i)//+zv_get_bought(i);
			if(bought)
			{
				zp_ammopacks_set(i, zp_ammopacks_get(i)+bought)
				zp_colored_print(i, "You have been refunded^3 %d ammopacks^1 to fend off the^3 Apocalypse!^1",bought)
			}			
			zp_colored_print(i, "All^3 Human Extra Items^1 are now^3 FREE!")
			continue;
		}
		
		set_user_maxspeed(i, get_user_maxspeed(i)*1.4)		
		
		if(zp_class_nemesis_get(i))
		{			
			continue;
		}
		set_user_health(i, floatround(float(get_user_health(i))*1.75))
	}
}

public replay()
{
	client_cmd(0,"mp3 play ^"/sound/ambience/gc_apocalypse.mp3^"")
}

// Custom Night Vision Task
public custom_nightvision_task()
{		
	NvgCounter++;
	if(NvgCounter>=201)
		NvgCounter=0;
		
	/*if(!(NvgCounter%59))
	{
		set_hudmessage(255,0,0, -1.0, 0.2, 1)
		ShowSyncHudMsg(0, HudSync, "The Apocalypse is here!")
	}*/
		
	for (new id=1;id<33;id++)
	{
		if(!is_user_connected(id))
			continue;		
		
		if(zp_is_nvg_active(id))
			continue;

		if(zp_is_blind(id))
			continue;
			
		if(!zp_core_is_zombie(id))
		{	
			if(NvgCounter==1)
			{
				UTIL_ScreenFade(id , {0, 0, 0},1.0, 1.0, 255,FFADE_OUT);
				continue;
			}
			
			if(NvgCounter==11)
			{
				UTIL_ScreenFade(id , {0, 0, 0},1.244, 0.0, 255,FFADE_IN);
				continue;
			}
			
			if(NvgCounter==21)
			{
				UTIL_ScreenFade(id , {255, 0, 0},1.0, 1.0, 50,FFADE_OUT);
				continue;
			}
			
			if(NvgCounter<31||NvgCounter>191)
			{
				continue;
			}				
			
			if(NvgCounter==191)
			{
				UTIL_ScreenFade(id , {255, 0, 0},1.0, 0.0, 50,FFADE_IN);
				continue;
			}
			
			UTIL_ScreenFade(id , {255, 0, 0},0.1, 0.1, 50);			
		}
	}
	
}

public zp_fw_gamemodes_end()
{		
	if(task_exists(TASK_NIGHTVISION))
		remove_task(TASK_NIGHTVISION);
		
	if(task_exists(TASK_REPLAY))
		remove_task(TASK_REPLAY)

	if(Active)
	{
		server_cmd("zp_lighting o")
		client_cmd(0, "mp3 stop")
	}
	Active=false;	
}

public zp_fw_core_infect_post(id,infector)
{
	if(!Active)
		return;

	if(!infector||id==infector)
	{		
		set_user_maxspeed(id, get_user_maxspeed(id)*1.4)
		set_user_health(id, floatround(float(get_user_health(id))*1.75))
	}
	else
	{
		set_user_health(infector,get_user_health(infector)+get_pcvar_num(cvar_infection_health))
	}
}

public fw_TakeDamage(victim, inflictor, attacker, Float:damage)
{
	if(!Active)
		return HAM_IGNORED;	
	
	if(is_user_connected(victim))
	{
	}
	else
	{
		new sz_classname[32] 
		entity_get_string( victim , EV_SZ_classname , sz_classname, 31 )			
		if(equali(sz_classname,"lasermine") ) 
		{
		}
		else
		if(equali(sz_classname,"amxx_pallets"))
		{
		}
		else
		if(equali(sz_classname,"rcbomb"))
		{
		}
		else	
			return HAM_IGNORED; 
	}
	
	if(!is_user_alive(attacker))
		return HAM_IGNORED;
		
	if(victim == attacker)
		return HAM_IGNORED;
	
	if(!zp_core_is_zombie(attacker))
		return HAM_IGNORED;
			
	if(damage==65.0)
	{
		SetHamParamFloat(4, 80.0)
		return HAM_IGNORED;
	}
	
	if(damage==15.0)
	{		
		SetHamParamFloat(4, 20.0)
	}
	
	return HAM_IGNORED;
		
}

public zp_fw_grenade_fire_pre(id)
{
	if(Active)
		return PLUGIN_HANDLED;
	
	return PLUGIN_CONTINUE;
}

public zp_fw_grenade_frost_pre(id)
{
	if(Active)
		return PLUGIN_HANDLED;
	
	return PLUGIN_CONTINUE;
}
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1033\\ f0\\ fs16 \n\\ par }
*/
